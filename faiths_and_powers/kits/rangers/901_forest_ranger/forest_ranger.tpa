
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//							FOREST RANGER
//__________________________________________________________________________________
//__________________________________________________________________________________

COPY_EXISTING ~clabrn01.2da~ ~override/d5rfor.2da~
	LPM remove_blank_lines
	APPEND_FILE ~faiths_and_powers/kits/rangers/901_forest_ranger/ranger_goodberry.txt~
	APPEND_FILE ~faiths_and_powers/kits/rangers/901_forest_ranger/ranger_enemies.txt~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory
	FOR (row = 3; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY row cols 0 ~row_name~ // read column value
	  PATCH_IF ~%row_name%~ STRING_EQUAL_CASE ~QD_MULTI~ BEGIN
	    SET patch_row = %row%
	  END
	  REMOVE_2DA_ROW patch_row cols
	END

//change ranger class text___________________________________________________________
//
ACTION_IF (FILE_EXISTS_IN_GAME ~clastext.2da~) BEGIN
  COPY_EXISTING ~clastext.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~text~ // read column value
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~RANGER~ BEGIN
	    SET patch_row = %row%
	  END
	END
	SET_2DA_ENTRY %patch_row% 3 cols RESOLVE_STR_REF (@90001)
	SET_2DA_ENTRY %patch_row% 5 cols RESOLVE_STR_REF (@90002)
	SET_2DA_ENTRY %patch_row% 4 cols RESOLVE_STR_REF (@90003)
  BUT_ONLY
END
ACTION_IF (FILE_EXISTS_IN_GAME ~sodcltxt.2da~) BEGIN
  COPY_EXISTING ~sodcltxt.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~text~ // read column value
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~RANGER~ BEGIN
	    SET patch_row = %row%
	  END
	END
	SET_2DA_ENTRY %patch_row% 3 cols RESOLVE_STR_REF (@90001)
	SET_2DA_ENTRY %patch_row% 5 cols RESOLVE_STR_REF (@90002)
	SET_2DA_ENTRY %patch_row% 4 cols RESOLVE_STR_REF (@90003)
  BUT_ONLY
END
//___________________________________________________________________________________


//NO FAVORED ENEMY MENU______________________________________________________________
//
OUTER_SET no_enemy_1 = RESOLVE_STR_REF (@90011)
OUTER_SET no_enemy_2 = RESOLVE_STR_REF (@90012)
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/haterace.2da~ ~override~
	REPLACE_TEXTUALLY ~abc~ ~%no_enemy_1%~
	REPLACE_TEXTUALLY ~xyz~ ~%no_enemy_2%~
//___________________________________________________________________________________


//____________________________________________________________________________________
//
ADD_KIT ~D5_RA_FOR~

//WEAPON PROFICIENCIES________________________________________________________________
//11                                    T   S           F   Q             S S
//10            L S                     W   C           L   U             W I
//9             A M             B L S   O   I   W       A   A             O N
//8             R A             A O H   H   M   A       I   R C   S       R G
//7             G L           M S N O   A   I   R     H L   T R L H     2 D L 2                 E E E E E E E E E E E
//6             E L       S   I T G R   N K T D H     A M   E O O O     H A E W E E E E E E E E X X X X X X X X X X X
//5             S S   S B P   S A S T   D A A A A   S L O   R S N R   S A N W E X X X X X X X X T T T T T T T T T T T
//4             W W   P L I   S R W S   E T R G M C P B R M S S G T D L N D E A T T T T T T T T R R R R R R R R R R R
//3             O O B E U K A I D O W A D A W G M L E E N A T B B B A I D S A P R R R R R R R R A A A A A A A A A A A
//2             R R O A N E X L S R O X S N A E E U A R I C A O O O R N E H P O A A A A A A A A 1 1 1 1 1 1 1 1 1 1 2
//1             D D W R T D E E W D R E W A K R R B R D N E F W W W T G D I O N 2 3 4 5 6 7 8 9 0 1 2 3 4 5 6 7 8 9 0
~D5_RA_FOR      2 2 2 2 2 2 2 2~
~D5_RA_FOR      2 2 2 2 2 2 2 2 2 2 2 2 0 0 2 2 2 2 2 0 2 2 2 2 2 2 2 2 2 0 2 2 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~

//MINIMUM KIT STATS_________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_RA_FOR          0   0   12  0   9   0~

//KIT STAT MODIFIERS________________________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_RA_FOR          0   0   0   0   0   0~

//REQUIREMENTS TO DUAL TO THIS KIT_________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_RA_FOR          0   0   17  0   0   0~

//REQUIREMENTS TO DUAL FROM THIS KIT________________________________________________
//                 STR DEX CON INT WIS CHR
~D5_RA_FOR          0   0   15  0   0   0~

//ALIGNMENT RESTRICTIONS____________________________________________________________
//                 LG LN LE NG TN NE CG CN CE
~D5_RA_FOR          1  1  0  1  1  0  1  0  0~

//DUAL CLASS OPTIONS________________________________________________________________
//                 FT CL MA TH DR RA
~D5_RA_FOR          0  1  0  0  0  0~

//KIT ABILITIES 2DA FILE____________________________________________________________
~override/d5rfor.2da~

//RACIAL KIT AVAILABILITY___________________________________________________________
~K_R_H    K_R_D   K_R_G   K_R_E   K_R_HE   K_R_HL   K_R_HO~

//UNUSABLE FLAGS AND KIT BASE CLASS_________________________________________________
//CLASSES: Mage = 1, Fighter = 2, Cleric=3, Thief = 4, Bard = 5
//         Paladin 6, Druid = 11, Ranger = 12, Sorcerer = 19
//         Monk = 20
~0x00004000 12~

//HIGH LEVEL ABILITIES ABBREVIATION_________________________________________________
~Ra0~

//TOB STARTING EQUIPMENT_____________________________________________________________
~LEAT14	* HELM07 BAG24 RING06 RING31 CLCK02 BOOT01 AMUL19 BRAC16 BELT06 AROW11,40 BULL03,40 BOLT06,40 POTN52,5 POTN04,2 POTN14,5 BOW18 SW1H39 STAF08~

//CHARACTER CREATION KIT DESCRIPTION________________________________________________
SAY @90101
SAY @90102
SAY @90103
//__________________________________________________________________________________

//EE KIT EXTRAS_____________________________________________________________________
//
ACTION_IF GAME_IS ~bgee bg2ee eet iwdee~ THEN BEGIN
	LAF fl#add_kit_ee
		INT_VAR
			briefdesc = RESOLVE_STR_REF (@90104)
		STR_VAR
			kit_name = D5_RA_FOR
			clswpbon = ~1 0 2~
	END
END
//____________________________________________________________________________________


//FOREST RANGER ABILITY FILES________________________________________________________
//
COPY_EXISTING ~sppr207.spl~ ~override/d5rnm01.spl~ // goodberry
	LPM spell_to_innate
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/d5enemf.spl~ ~override~
	SAY NAME1 @6
	SAY UNIDENTIFIED_DESC @6
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/d5enemfa.eff~ ~override~
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/d5enemfb.eff~ ~override~
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/d5enemfc.eff~ ~override~
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/d5enemfd.eff~ ~override~
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/d5enemfe.eff~ ~override~
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/d5enemff.eff~ ~override~
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/d5enemfg.eff~ ~override~
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/d5enemfh.eff~ ~override~
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/d5enemfi.eff~ ~override~
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/d5enemfj.eff~ ~override~
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/d5enemfk.eff~ ~override~
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/d5enemfl.eff~ ~override~
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/d5enemfm.eff~ ~override~
COPY ~faiths_and_powers/kits/rangers/901_forest_ranger/d5enemfn.eff~ ~override~
//___________________________________________________________________________________

//REPLACE VANILLA KIT WITH THIS KIT__________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~d5_rangers.d5~ BEGIN
  ACTION_FOR_EACH race IN ~D~ ~E~ ~G~ ~H~ ~HE~ ~HL~ ~HO~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~K_R_%race%.2da~ BEGIN
	  COPY_EXISTING ~K_R_%race%.2da~ ~override~
		COUNT_2DA_COLS cols
		READ_2DA_ENTRIES_NOW rows cols
		SET rows_removed = 0
		FOR (row = 1; row < rows; ++row) BEGIN
		  READ_2DA_ENTRY_FORMER rows row 1 kit
		  PATCH_IF (%kit% = 0) BEGIN
			REMOVE_2DA_ROW (row - rows_removed) cols
//			SET rows_removed += 1
		  END
		END
	  BUT_ONLY
	END
  END
END
//___________________________________________________________________________________

//REPLACE BASE RANGER WITH THIS KIT__________________________________________________
//
ACTION_IF MOD_IS_INSTALLED ~faiths_and_powers.tp2~ ~103~ BEGIN
	COPY_EXISTING ~kitlist.2da~ ~override~
		COUNT_2DA_ROWS ~7~ "kit_row"
	OUTER_SET kit_row -= 2
	COPY_EXISTING ~K_R_D.2da~ ~override~
			~K_R_E.2da~ ~override~
			~K_R_G.2da~ ~override~
			~K_R_H.2da~ ~override~
			~K_R_HE.2da~ ~override~
			~K_R_HL.2da~ ~override~
			~K_R_HO.2da~ ~override~
		REPLACE_TEXTUALLY ~[ %TAB%]0~ ~%TAB%%kit_row%~
	BUT_ONLY
END
//___________________________________________________________________________________


//SWITCH NPC RANGERS TO THIS KIT_____________________________________________________
//
//___________________________________________________________________________________


//DEFINE SPHERE ACCESS_______________________________________________________________
//
 LAF ~DEFINE_FNP_KIT_INFO~
  INT_VAR
    c_ranger 		= 1
  STR_VAR
    clab_name 		= ~d5rfor~
	race 			= ~all~
	s_Life 			= ~ranger~
	s_Death 		= ~x~
	s_Benediction 	= ~x~
	s_Destruction 	= ~x~
	s_Protection 	= ~ranger~
	s_War 			= ~x~
	s_Knowledge 	= ~ranger~
	s_Deception 	= ~x~
	s_Thought 		= ~x~
	s_Dread 		= ~x~
	s_Vigor 		= ~ranger~
	s_Affliction 	= ~x~
	s_Animal 		= ~ranger~
	s_Plant 		= ~ranger~
	s_Earth 		= ~x~
	s_Water 		= ~x~
	s_Air 			= ~x~
	s_Fire 			= ~x~
	s_Light 		= ~ranger~
	s_Shadow 		= ~x~
	s_Magic 		= ~x~
END
//__________________________________________________________________________________

