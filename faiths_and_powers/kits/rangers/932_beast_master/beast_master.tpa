
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						REVISED BEASTMASTER
//__________________________________________________________________________________
//__________________________________________________________________________________



//ADD TO KIT MENUS__________________________________________________________________
//
ACTION_IF MOD_IS_INSTALLED ~faiths_and_powers.tp2~ ~103~ /* FILE_EXISTS_IN_GAME ~d5_menu.d5~ */ BEGIN
  ACTION_FOR_EACH menu IN ~K_R_D~ ~K_R_E~ ~K_R_G~ ~K_R_H~ ~K_R_HE~ ~K_R_HL~ ~K_R_HO~ BEGIN
	ACTION_IF FILE_EXISTS_IN_GAME ~%menu%.2da~ BEGIN
	  APPEND ~%menu%.2da~ ~1	9~
	END
  END
END
//___________________________________________________________________________________


//BEASTMASTER WEAPONS____________________________________________________________
//
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~ // copies all item files
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // avoid crashing on empty items
		READ_SHORT ~0x1c~ type
		PATCH_IF (type = ~16~) BEGIN // daggers
			READ_BYTE 0x2b kit 
			WRITE_BYTE 0x2b (kit BAND 0b11111101) // makes usable by beastmasters
		END
	END
BUT_ONLY
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~ // copies all item files
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // avoid crashing on empty items
		READ_SHORT ~0x1c~ type 
		PATCH_IF (type = ~25~) BEGIN // axes
			READ_BYTE 0x2b kit
			WRITE_BYTE 0x2b (kit BAND 0b11111101) // makes usable by beastmasters
		END
	END
BUT_ONLY
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~ // copies all item files
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // avoid crashing on empty items
		READ_SHORT ~0x1c~ type
		PATCH_IF (type = 29) BEGIN // spears
			READ_BYTE 0x2b kit 
			WRITE_BYTE 0x2b (kit BAND 0b11111101) // makes usable by beastmasters
		END
	END
BUT_ONLY
COPY_EXISTING ~weapprof.2da~ ~override~
	SET_2DA_ENTRY_LATER ~#weapprof~ 31 38 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 32 38 0
	SET_2DA_ENTRY_LATER ~#weapprof~ 33 38 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 34 38 3
	SET_2DA_ENTRY_LATER ~#weapprof~ 14 38 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 18 38 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 21 38 2
	SET_2DA_ENTRY_LATER ~#weapprof~ 26 38 0
	SET_2DA_ENTRIES_NOW ~#weapprof~ 1
BUT_ONLY
//___________________________________________________________________________________


//UPDATE EXISTING STRINGS__________________________________________________________
// 
ACTION_IF (FILE_EXISTS_IN_GAME ~clastext.2da~) BEGIN
  COPY_EXISTING ~clastext.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	COUNT_2DA_ROWS cols rows // amount of rows
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~text~ // read column value
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~BEAST_MASTER~ BEGIN
	    SET patch_row = %row%
	  END
	END
	SET_2DA_ENTRY (%patch_row%) 4 cols RESOLVE_STR_REF (@93200)
  BUT_ONLY
END
ACTION_IF (FILE_EXISTS_IN_GAME ~sodcltxt.2da~) BEGIN
  COPY_EXISTING ~sodcltxt.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	COUNT_2DA_ROWS cols rows // amount of rows
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~text~ // read column value
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~BEAST_MASTER~ BEGIN
	    SET patch_row = %row%
	  END
	END
	SET_2DA_ENTRY (%patch_row%) 4 cols RESOLVE_STR_REF (@93200)
  BUT_ONLY
END
ACTION_IF (FILE_EXISTS_IN_GAME ~kitlist.2da~) BEGIN
  COPY_EXISTING ~kitlist.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	COUNT_2DA_ROWS cols rows // amount of rows
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 1 ~text~ // read column value
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~BEASTMASTER~ BEGIN
	    SET patch_row = %row%
	  END
	END
	SET_2DA_ENTRY (%patch_row%) 4 cols RESOLVE_STR_REF (@93200)
  BUT_ONLY
END
//ACTION_IF ENGINE_IS ~tob~ BEGIN
//	STRING_SET 25212 @93200
//END
//__________________________________________________________________________________


//NO FALLING/MORE ALIGNMENTS________________________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN
	APPEND ~fallen.2da~ ~BEASTMASTER  0~
END

ACTION_IF FILE_EXISTS_IN_GAME ~alignmnt.2da~ BEGIN
  COPY_EXISTING ~alignmnt.2da~ ~override~
	COUNT_2DA_COLS cols // amount of columns
	READ_2DA_ENTRIES_NOW rows cols // read all file into memory  
	FOR (row = 1; row < rows; ++row) BEGIN // iterate over rows
	  READ_2DA_ENTRY_FORMER rows row 0 ~text~ // read column value
	  PATCH_IF ~%text%~ STRING_EQUAL_CASE ~BEASTMASTER~ BEGIN
	    SET patch_row = %row%
	  END
	END
	FOR (col = 1; col < cols; ++col) BEGIN // iterate over columns
		SET_2DA_ENTRY %patch_row% col cols 1
	END
  BUT_ONLY
END
//___________________________________________________________________________________


//BEASTMASTER SHAPECHANGING + SPIRIT WOLF____________________________________________
//
COPY ~faiths_and_powers/kits/dervishes/932_beast_master/d5_rnnf.spl~ ~override~
	SAY NAME1 @93201
	SAY UNIDENTIFIED_DESC @93201
COPY ~faiths_and_powers/kits/dervishes/932_beast_master/d5_rnwlf.spl~ ~override~
	SAY NAME1 @93202
	SAY UNIDENTIFIED_DESC @93202
COPY ~faiths_and_powers/kits/dervishes/932_beast_master/d5_rnww.spl~ ~override~
	SAY NAME1 @93203
	SAY UNIDENTIFIED_DESC @93203
COPY ~faiths_and_powers/kits/dervishes/932_beast_master/d5_clwlf.itm~ ~override~
	SAY NAME1 @93204
	SAY UNIDENTIFIED_DESC @93204
COPY ~faiths_and_powers/kits/dervishes/932_beast_master/d5_clww1.itm~ ~override~
	SAY NAME1 @93204
	SAY UNIDENTIFIED_DESC @93204
COPY ~faiths_and_powers/kits/dervishes/932_beast_master/d5_clww2.itm~ ~override~
	SAY NAME1 @93204
	SAY UNIDENTIFIED_DESC @93204
COPY ~faiths_and_powers/kits/dervishes/932_beast_master/d5_wolf.cre~ ~override~
	SAY NAME1 @93205
COPY ~faiths_and_powers/kits/dervishes/932_beast_master/d5_dire.cre~ ~override~
	SAY NAME1 @93206
COPY ~faiths_and_powers/kits/dervishes/932_beast_master/d5_ww01.cre~ ~override~
	SAY NAME1 @93207
COPY ~faiths_and_powers/kits/dervishes/932_beast_master/d5_ww02.cre~ ~override~
	SAY NAME1 @93208
COPY ~faiths_and_powers/kits/dervishes/932_beast_master/d5_ww03.cre~ ~override~
	SAY NAME1 @93209
ACTION_IF (ENGINE_IS ~tob~) BEGIN
	COPY_EXISTING ~clabrn04.2da~ ~override~
		REPLACE_TEXTUALLY ~GA_SPCL342~ ~****      ~
		LPM remove_blank_lines
		APPEND_FILE ~faiths_and_powers/kits/dervishes/932_beast_master/beastmasterog.txt~
	BUT_ONLY
END
ELSE BEGIN
	COPY_EXISTING ~clabrn04.2da~ ~override~
		REPLACE_TEXTUALLY ~GA_SPCL342~ ~****      ~
		LPM remove_blank_lines
		APPEND_FILE ~faiths_and_powers/kits/dervishes/932_beast_master/beastmaster.txt~
	BUT_ONLY
END
//__________________________________________________________________________________


//DEFINE SPHERE ACCESS______________________________________________________________
//
 LAF ~DEFINE_FNP_KIT_INFO~
  INT_VAR
    c_ranger 		= 1
  STR_VAR
    clab_name = EVAL ~CLABRN04~
	race 			= ~all~
	s_Life 			= ~ranger~
	s_Death 		= ~x~
	s_Benediction 	= ~x~
	s_Destruction 	= ~x~
	s_Protection 	= ~x~
	s_War 			= ~x~
	s_Knowledge 	= ~ranger~
	s_Deception 	= ~x~
	s_Thought 		= ~x~
	s_Dread 		= ~x~
	s_Vigor 		= ~ranger~
	s_Affliction 	= ~x~
	s_Animal 		= ~ranger~
	s_Plant 		= ~x~
	s_Earth 		= ~x~
	s_Water 		= ~x~
	s_Air 			= ~x~
	s_Fire 			= ~x~
	s_Light 		= ~x~
	s_Shadow 		= ~x~
	s_Magic 		= ~x~
END
//__________________________________________________________________________________

